##
БАГИ:
Если комментарий находится на той же строке что и инструкция, он требует обязательной точки с запятой, пример:
var $x, 0 #БАГА
так работает:
var $x, 0; #БАГА
##

plztrustme true

print '=======================================\n'
print 'Добро пожаловать в игру "Угадай число"!\n'
print '=======================================\n'
while (>change true), 
{
  var $x, 0
  var $min, 1            
  var $max, (rand $, 100, 10000)
  var $tries, (>to 'ntg', (>calc '*', (>calc 'log', $max, 2), 1.2))
  var $secret, (rand $, $min, $max)
  print 'Загадано число от ', $min, ' до ', $max, ' попробуйте угадать!\n'

  while (>comp '!=', $x, $secret), 
  {
    print 'Введите число (оcталось ', $tries, ' попыток): '
    while (>change true), 
    {
      input $x
      if (castchk 'ntg', $, $x), 
      {
        to 'ntg', $x
        break 1
      }, 
      {
        print 'Вы ввели не число, попробуйте еще раз: '
      }
    }
    
    if (>comp '==', $tries, 1), 
    {
      print 'Попыток больше не осталось. Вы проиграли!\n'
      break
    }

    if (>comp '<', $x, $secret), 
    {
      print 'Загаданное число больше!\n'
    }, 
    {
      if (>comp '>', $x, $secret), 
      {
        print 'Загаданное число меньше!\n'
      }
    }
    calc 'dec', $tries
  }
  
  if (>comp '==', $x, $secret), 
  {
    print 'Вы угадали число! Вы выиграли!\n'
  }
  free $x, $tries, $secret, $min, $max

  print '=======================================\n'
  print '\nХотите сыграть еще раз? (y/n)? '
  >input
  if (>>comp '!=', 'y'), 
  {
    print '=======================================\n'
    print 'Спасибо за игру! Всего доброго!\n'
    print '=======================================\n'
    break
  }
}

>end

#REPL
plzdontcrash true

print '♥️ =====================================================♥️\n' 
print '|CHRYSALIX REPL STARTED                                 |\n' 
print '♥️ =====================================================♥️\n'

var $reg__34567uhgfd
var $res__34567uhgfd

label &loop__34567uhgfd
    print ' $ = ', $res__34567uhgfd
    change $res__34567uhgfd, nil
    print ':'
    input $reg__34567uhgfd
    calc '+', $reg__34567uhgfd, $reg__34567uhgfd, '\n'
    pe $res__34567uhgfd, $reg__34567uhgfd
jump &loop__34567uhgfd

>end

