plztrustme true

print '=======================================\n'
print 'Добро пожаловать в игру "Угадай число"!\n'
print '=======================================\n'

while true, {
	var $min, 1 #Минимальное загадываемое число
	var $max_min, 10
	var $max_max, 100
	var $tries_multiplier, 2
	print 'Выберите сложность (e - легко, m - средне, h - сложно, eh - очень сложно): '
	while true, {
		>input
		>switch 'e', {
			break
		},
		'm', {
			change $min, 10
			change $max_min, 100
			change $max_max, 10000
			change $tries_multiplier, 1.5
			break
		},
		'h', {
		 	change $min, 100
			change $max_min, 10000
			change $max_max, 1000000
			change $tries_multiplier, 1.2
			break
		 },
		 'eh', {	
		 	change $min, 10000
			change $max_min, 1000000
			change $max_max, 1000000000
			change $tries_multiplier, 1.1
			break
		 },
		 {
		 	print 'Введена неверная сложность, введите еще раз: '
		 }
  	}
  	
  	var $x, 0 #Переменная с угадываемым числом          
  	var $max, (>rand $max_min, $max_max)  #Максимальное загадываемое число, выбирается случайно в зависимости от сложности
  	#Количество попыток, логарифм по основанию 2 от максимального числа, умноженный на множитель попыток
  	>calc log, $max, 2; >>calc *, $tries_multiplier; >to 'ntg'
  	var $tries, $
  	var $secret, (>rand $min, $max)  #загаданное число
  	
 	print 'Загадано число от ', $min, ' до ', $max, ' попробуйте угадать!\n'
  	
  	#Главный цикл. Выходим из него, если игрок угадал
	while (>comp '!=', $x, $secret), {
  		#Даем игроку ввести число. Если игрок ввел не число, повторяем ввод
		print 'Введите число (оcталось ', $tries, ' попыток): '
		while true, {
			input $x
			if (>castchk 'ntg', $x), {
				to 'ntg', $x
				break 1
			}, {
				print 'Вы ввели не число, попробуйте еще раз: '
			}
		}
    
    		#Если попыток не осталось
		if (>comp '==', $tries, 1), {
			print 'Попыток больше не осталось. Вы проиграли!\n'
			break
		}

		#Если число больше
		if (>comp '<', $x, $secret), {
			print 'Загаданное число больше!\n'
		}, {
			#Если число меньше
			if (>comp '>', $x, $secret), {
				print 'Загаданное число меньше!\n'
			}
		}
    
		#Уменьшаем число попыток
		calc dec, $tries
	}
  
	#Поздравление, если число угадано
	if (>comp '==', $x, $secret), {
		print 'Вы угадали число! Вы выиграли!\n'
	}
	
	free $x, $tries, $secret, $min, $max, $max_min, $max_max, $tries_multiplier

	#Предложение сыграть еще раз
	print '=======================================\n'
	print '\nХотите сыграть еще раз? (y/n)? '
	>input
	if (>>comp '!=', 'y'), {
		print '=======================================\n'
		print 'Спасибо за игру! Всего доброго!\n'
		print '=======================================\n'
		break
	}
}
end 0
